#lang racket
(provide F F-pre ğ‘­ ğ‘­ğ’“ lookup ext)
(require redex/reduction-semantics
         (only-in "../extort/semantics.rkt" E ğ‘¬))

; for use in presentations (informally noting x can't be let, etc.)
(define-extended-language F-pre E
  (e ::= .... x (let ((x e)) e))
  (x ::= variable))

;; the real grammar language
(define-extended-language F F-pre
  (x ::= variable-not-otherwise-mentioned)
  (r ::= ((x i) ...)))

(module+ test
  (test-equal (redex-match? F e (term x)) #t)
  (test-equal (redex-match? F e (term let)) #f)
  (test-equal (redex-match? F e (term (let ((x 1)) x))) #t)
  (test-equal (redex-match? F e (term (let ((let 1)) 3))) #f))

(module+ test
  (test-equal (redex-match? F-pre e (term x)) #t)
  (test-equal (redex-match? F-pre e (term let)) #t)
  (test-equal (redex-match? F-pre e (term (let ((x 1)) x))) #t)
  (test-equal (redex-match? F-pre e (term (let ((let 1)) 3))) #t))

(define-judgment-form F
  #:contract (ğ‘­ e a)
  #:mode (ğ‘­ I O)
  [(ğ‘­ğ’“ e () a)
   ----------
   (ğ‘­ e a)])

(define-judgment-form F  
  #:contract (ğ‘­ğ’“ e r a)
  #:mode (ğ‘­ğ’“ I I O)

  ;; New
  [(where v (lookup r x))
   -----------
   (ğ‘­ğ’“ x r v)]

  [(ğ‘­ğ’“ e_0 r v_0) (where r_1 (ext r x v_0)) (ğ‘­ğ’“ e_1 r_1 v_1)
   -----
   (ğ‘­ğ’“ (let ((x e_0)) e_1) r v_1)]

  ;; Extension of Dupe's semantics
  [-----------
   (ğ‘­ğ’“ v r v)]

  [(ğ‘­ğ’“ e_0 r i_0) (where i_1 ,(+ (term i_0) 1))
   -----------
   (ğ‘­ğ’“ (add1 e_0) r i_1)]
  
  [(ğ‘­ğ’“ e_0 r i_0) (where i_1 ,(- (term i_0) 1))
   -----------
   (ğ‘­ğ’“ (sub1 e_0) r i_1)]

  [(ğ‘­ğ’“ e_0 r i) (side-condition ,(= (term i) 0))
   -----------
   (ğ‘­ğ’“ (zero? e_0) r #t)]

  [(ğ‘­ğ’“ e_0 r i) (side-condition ,(!= (term i) 0))
   -----------
   (ğ‘­ğ’“ (zero? e_0) r #f)]

  [(ğ‘­ğ’“ e_0 r v_0) (is-true v_0) (ğ‘­ğ’“ e_1 r v_1)
   --------
   (ğ‘­ğ’“ (if e_0 e_1 e_2) r v_1)]
  
  [(ğ‘­ğ’“ e_0 r v_0) (is-false v_0) (ğ‘­ğ’“ e_2 r v_2)
   --------
   (ğ‘­ğ’“ (if e_0 e_1 e_2) r v_2)]

  ;; Extension of Extort's semantics
  [--------
   (ğ‘­ğ’“ (add1 b) r err)]

  [-----------
   (ğ‘­ğ’“ (sub1 b) r err)]

  [-----------
   (ğ‘­ğ’“ (zero? b) r err)]

  [(ğ‘­ğ’“ e r err)
   -----------
   (ğ‘­ğ’“ (zero? e) r err)]

  [(ğ‘­ğ’“ e r err)
   -----------
   (ğ‘­ğ’“ (add1 e) r err)]

  [(ğ‘­ğ’“ e r err)
   -----------
   (ğ‘­ğ’“ (sub1 e) r err)]

  [(ğ‘­ğ’“ e r err)
   -----------
   (ğ‘­ğ’“ (if e e_0 e_1) r err)]

  ;; Error propagation for Let
  [(ğ‘­ğ’“ e_0 r err)
   -----------
   (ğ‘­ğ’“ (let ((x e_0)) e_1) r err)]

  [(ğ‘­ğ’“ e_0 r v_0) (ğ‘­ğ’“ e_1 r err)
   -----------
   (ğ‘­ğ’“ (let ((x e_0)) e_1) r err)])


(define-judgment-form F
  #:mode (is-true I)
  #:contract (is-true v)
  [-----------
   (is-true #t)]
  [----------
   (is-true i)])

(define-judgment-form F
  #:mode (is-false I)
  #:contract (is-false v)
  [-----------
   (is-false #f)])

(define (!= n m)
  (not (= n m)))

(define-metafunction F
  ext : r x i -> r
  [(ext ((x_0 i_0) ...) x i)
   ((x i) (x_0 i_0) ...)])

(define-metafunction F
  lookup : r x -> i or undefined
  [(lookup () x) undefined]
  [(lookup ((x i) (x_1 i_1) ...) x) i]
  [(lookup ((x_0 i_0) (x_1 i_1) ...) x)
   (lookup ((x_1 i_1) ...) x)])

(module+ test
  (test-judgment-holds (ğ‘­ 7 7))
  (test-judgment-holds (ğ‘­ (add1 7) 8))

  (test-judgment-holds (ğ‘­ (add1 #f) err))
  
  (test-judgment-holds (ğ‘­ (let ((x 7)) 8) 8))
  (test-judgment-holds (ğ‘­ (let ((x 7)) x) 7))
  (test-judgment-holds (ğ‘­ (let ((x 7)) (add1 x)) 8))
  (test-judgment-holds (ğ‘­ (sub1 (let ((x 7)) (add1 x))) 7))
  (test-judgment-holds (ğ‘­ (sub1 (let ((x 7))
                                  (let ((y x))
                                    (add1 x))))
                          7))
  (test-judgment-holds (ğ‘­ (sub1 (let ((x 7))
                                  (let ((x 8))
                                    (add1 x))))
                          8)))

