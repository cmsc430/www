#lang racket
(provide D-concrete D ğ‘« is-true is-false)
(require redex/reduction-semantics
         (only-in "../con/semantics.rkt" C C-concrete))

(define-extended-language D-concrete C-concrete
  (e ::= .... boolean (if e e e) (zero? e)))
  
(define-extended-language D C
  (e ::= .... (Bool b) (If e e e) (Zero? e))
  (v ::= i b)
  (b ::= #t #f))

(define-judgment-form D
  #:mode (ğ‘« I O)
  #:contract (ğ‘« e v)  
  [--------
   (ğ‘« (Int i) i)]

  [--------
   (ğ‘« (Bool b) b)]

  [(ğ‘« e_0 i_0) (where i_1 ,(+ (term i_0) 1))
   -----------
   (ğ‘« (Add1 e_0) i_1)]

  [(ğ‘« e_0 i_0) (where i_1 ,(- (term i_0) 1))
   -----------
   (ğ‘« (Sub1 e_0) i_1)]

  [(ğ‘« e_0 i) (side-condition ,(= (term i) 0))
   -----------
   (ğ‘« (Zero? e_0) #t)]

  [(ğ‘« e_0 i) (side-condition ,(!= (term i) 0))
   -----------
   (ğ‘« (Zero? e_0) #f)]

  [(ğ‘« e_0 v_0) (is-true v_0) (ğ‘« e_1 v_1)
   --------
   (ğ‘« (If e_0 e_1 e_2) v_1)]
  
  [(ğ‘« e_0 v_0) (is-false v_0) (ğ‘« e_2 v_2)
   --------
   (ğ‘« (If e_0 e_1 e_2) v_2)])

(module+ test
  (test-judgment-holds (ğ‘« (Int 7) 7))
  (test-judgment-holds (ğ‘« (Bool #f) #f))
  (test-judgment-holds (ğ‘« (Bool #t) #t))
  (test-judgment-holds (ğ‘« (Add1 (Int 8)) 9))
  (test-judgment-holds (ğ‘« (Sub1 (Int 8)) 7))

  (test-judgment-holds (ğ‘« (If (Bool #f) (Int 3) (Int 4)) 4))
  (test-judgment-holds (ğ‘« (If (Bool #t) (Int 3) (Int 4)) 3))
  (test-judgment-holds (ğ‘« (Zero? (Int 0)) #t))
  (test-judgment-holds (ğ‘« (Zero? (Int 1)) #f))
  (test-judgment-holds (ğ‘« (If (Zero? (Int 0)) (Int 3) (Int 4)) 3))
  (test-judgment-holds (ğ‘« (If (Zero? (Int 1)) (Int 3) (Int 4)) 4)))

(define-judgment-form D
  #:mode (is-true I)
  #:contract (is-true v)
  [-----------
   (is-true #t)]
  [----------
   (is-true i)])

(define-judgment-form D
  #:mode (is-false I)
  #:contract (is-false v)
  [-----------
   (is-false #f)])

(define (!= n m)
  (not (= n m)))
